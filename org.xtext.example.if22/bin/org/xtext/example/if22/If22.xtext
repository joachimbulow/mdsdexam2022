grammar org.xtext.example.if22.If22 with org.eclipse.xtext.common.Terminals

generate if22 "http://www.xtext.org/example/if22/If22"

Program:
	{Program} 'story' name=ID (externalFunctions+=Function)* (scenarios+=Scenario)*;

Function:
	'function' name=ID '(' inputType=Exp ')' ':' returnType=Type;

Scenario:
	'scenario' name=ID ('(' parameters+=ScenarioParameter (',' parameters+=ScenarioParameter)* ')')? '{'
	(variableDeclarations+=VariableDeclaration)*
	(statements+=Statement)*
	'}';

ScenarioParameter:
	parameter=Exp ':' type=Exp
;

ScenarioParameterInput:
 parameter=Exp
;

VariableDeclaration:
	'var' name=ID ':' type=Type;

Statement:
	Announcement | Question | End;

Announcement:
	'announce' name=ID exp=Exp (targets+=Target)+;

Question:
	'question' name=ID qString=Exp 'as' qType=Exp ('in' reffedVar=[VariableDeclaration])? (targets+=Target)+;

End:
	'end' name=ID exp=Exp?;

Target:
	{Target} 'to' destination=[TargetDestination] ('(' parameterInputs+=ScenarioParameterInput (',' parameterInputs+=ScenarioParameterInput)* ')')? ('if' targetCheck=Exp)? ('{' (endTargets+=EndingTarget)+ '}')?;

Exp returns Expression:
	Primary
	(({Logic.left=current} operator=('==' | '!=' | '<' | '>' | '<=' | '>=' | '&&' | '||') | {Math.left=current}
	operator=('+' | '-' | '*'
	| '/') | {TextExp.left=current} '&') right=Primary)*;

Primary returns Expression:
	Parenthesis | Type | {This} value='this' | {EXPSTRING} value=STRING | {EXPINT} value=INT | {EXPBOOL} value=BOOLEAN |
	{ID} value=ID | ExternalFunctionCall;

ExternalFunctionCall returns Expression:
	{ExternalFunctionCall} efName=ID '(' efParameter=Exp ')';

Parenthesis returns Expression:
	{Parenthesis} '(' exp=Exp ')';

Type:
	{TypeBoolean} value='boolean' | {TypeText} value='text' | {TypeNumber} value='number';

terminal BOOLEAN:
	'true' | 'false';
	
TargetDestination:
	Statement | Scenario
;

// Scoped cross references
EndingTarget:
	{EndingTarget}'on' callableEnd=[End] 'to' selfdefinedEnd=[End]
;
